plugins {
    id 'com.android.application'
}

android {
    namespace 'com.voc.honkai_stargazer'
    compileSdk 33

    defaultConfig {
        applicationId "com.voc.honkai_stargazer"
        minSdk 26
        targetSdk 33
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    def versionPropsFile = rootProject.file('version.properties')
    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        def code = versionProps['VERSION_CODE'].toInteger() + 1
        versionProps['VERSION_CODE'] = code.toString()
        versionProps.store(versionPropsFile.newWriter(), null)

        flavorDimensions "version"
        productFlavors {
            // For Develop Testing
            dev {
                versionCode = code
                versionNameSuffix "-dev" + " ("+code+")"
                applicationId "com.voc.honkai_stargazer"


            }
            // For Google Play Store
            gp{
                versionCode = code
                versionNameSuffix "-prod" + " ("+code+")"
                applicationId "com.voc.honkai_stargazer_gp"
                bundle{
                    language {
                        enableSplit = false;
                    }
                }
            }
            // For Translator and Beta Tester
            beta {
                versionCode = code
                versionNameSuffix "-beta" + " ("+code+")"
                applicationId "com.voc.honkai_stargazer_beta"
            }
        }
    }

    buildTypes {
        debug{
            ext.enableBugsnag = false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            packagingOptions {
                exclude 'META-INF/LICENSE'
                exclude 'META-INF/LICENSE-FIREBASE.txt'
                exclude 'META-INF/NOTICE'
                exclude 'META-INF/DEPENDENCIES'
            }
            defaultConfig {
                multiDexEnabled true
            }
        }

        android.applicationVariants.all { variant ->
            variant.outputs.all {
                outputFileName = "honkai_stargazer_${variant.versionName}.apk"
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "androidx.compose.material3:material3:1.1.0"
    implementation "androidx.compose.material3:material3-window-size-class:1.1.0"
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.github.bumptech.glide:glide:4.15.1'
    implementation 'com.github.ome450901:SimpleRatingBar:1.5.1'

    implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.8.0"))

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

